= jenkins backup

. GitHubdp 백업파일을 저장할 repository 생성(e.g. `wicksome/jenkins-backup`)
. 백업 파일 사이즈에 제한될 수 있으므로 `.gitattributes` 추가(commit/push)
+
[source]
----
$ git lfs track "*.tar.gz"
$ ls
.gitattributes
----
. SSH 키 생성
+
[source, bash]
----
$ ssh-keygen -t rsa -f jenkins_backup
$ ls
jenkins_backup  jenkins_backup.pub
----
. 공개키(`jenkins_backup.pub`)는 repo에 등록
+
Settings → Deploy keys → Add deploy key
. 비공개키(`jenkins_backup`)는 jenkins에 등록
+
Add Credentials → Kind: `SSH Username with private key` → ID: `jenkins-backup`
. Jenkinsfile을 활용하여 백업 작업 등록
+
[source, groovy]
----
pipeline {
    agent { label 'master' }
    environment {
        BACKUP_DATETIME = new Date().format('yyyyMMddHHmmss', TimeZone.getTimeZone('Asia/Seoul'))
    }
    triggers {
        cron('0 3 * * *')
    }
    stages {
        stage('Checkout') {
            steps {
                checkout([
                    $class           : 'GitSCM',
                    branches         : [[name: 'refs/tags/0.1.9']],
                    userRemoteConfigs: [[url: 'https://github.com/sue445/jenkins-backup-script.git']]
                ])
            }
        }

        stage('Backup') {
            when { expression { fileExists('./jenkins-backup.sh') } }
            steps {
                sh './jenkins-backup.sh ${JENKINS_HOME} cicd_${BACKUP_DATETIME}.tar.gz'
            }
        }

        stage('Upload') {
            steps {
                // git clone
                checkout([
                    $class           : 'GitSCM',
                    branches         : [[name: 'refs/remotes/origin/master']],
                    userRemoteConfigs: [[url: 'git@github.com:wicksome/jenkins-backup.git', credentialsId: 'jenkins-backup']]
                ])

                // setting git config
                sh '''
                    git config user.name 'JENKINS\'
                    git config user.email 'noreplay@nowhere'
                '''

                // rotate backup files
                sh 'find . -type f -name "cicd_*" | grep -v "${BACKUP_DATETIME}" | xargs rm -f'

                // git commit
                sh '''
                    git add cicd*
                    git ls-files --deleted | xargs git add
                    git commit -m "Backup the JENKINS_HOME of CI/CD"
                '''

                // git push
                sshagent(['jenkins-backup']) {
                    sh("""
                        #!/usr/bin/env bash
                        set +x
                        export GIT_SSH_COMMAND="ssh -oStrictHostKeyChecking=no"
                        git push origin HEAD:master
                    """)
                }
            }
        }
    }
    post {
        failure {
            script {
                def mailRecipients = 'opid911@gmail.com'
                def jobName = currentBuild.fullDisplayName

                emailext body: '''${SCRIPT, template="groovy-html.template"}''',
                    mimeType: 'text/html',
                    subject: "${jobName} 실패",
                    to: "${mailRecipients}",
                    replyTo: "${mailRecipients}",
                    recipientProviders: [[$class: 'CulpritsRecipientProvider']]
            }
        }
    }
}
----
